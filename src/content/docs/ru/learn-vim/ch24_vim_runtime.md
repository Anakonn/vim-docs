---
description: В этой главе рассматриваются пути выполнения Vim, их назначение и использование
  для настройки редактора, включая плагины и специальные директории.
title: Ch24. Vim Runtime
---

В предыдущих главах я упоминал, что Vim автоматически ищет специальные пути, такие как `pack/` (глава 22) и `compiler/` (глава 19) внутри директории `~/.vim/`. Это примеры путей выполнения Vim.

У Vim есть больше путей выполнения, чем эти два. В этой главе вы получите общее представление о этих путях выполнения. Цель этой главы — показать вам, когда они вызываются. Знание этого позволит вам лучше понять и настроить Vim.

## Путь выполнения

На машине с Unix одним из ваших путей выполнения Vim является `$HOME/.vim/` (если у вас другая ОС, такая как Windows, ваш путь может быть другим). Чтобы увидеть, какие пути выполнения существуют для разных ОС, посмотрите `:h 'runtimepath'`. В этой главе я буду использовать `~/.vim/` в качестве стандартного пути выполнения.

## Скрипты плагинов

У Vim есть путь выполнения плагинов, который выполняет любые скрипты в этой директории каждый раз, когда запускается Vim. Не путайте название "плагин" с внешними плагинами Vim (такими как NERDTree, fzf.vim и т.д.).

Перейдите в директорию `~/.vim/` и создайте директорию `plugin/`. Создайте два файла: `donut.vim` и `chocolate.vim`.

Внутри `~/.vim/plugin/donut.vim`:

```shell
echo "donut!"
```

Внутри `~/.vim/plugin/chocolate.vim`:

```shell
echo "chocolate!"
```

Теперь закройте Vim. В следующий раз, когда вы запустите Vim, вы увидите как `"donut!"`, так и `"chocolate!"`. Путь выполнения плагинов может быть использован для инициализационных скриптов.

## Обнаружение типа файла

Прежде чем начать, чтобы убедиться, что эти обнаружения работают, убедитесь, что ваш vimrc содержит хотя бы следующую строку:

```shell
filetype plugin indent on
```

Посмотрите `:h filetype-overview` для получения дополнительного контекста. По сути, это включает обнаружение типа файла в Vim.

Когда вы открываете новый файл, Vim обычно знает, какой это файл. Если у вас есть файл `hello.rb`, выполнение `:set filetype?` возвращает правильный ответ `filetype=ruby`.

Vim знает, как обнаруживать "общие" типы файлов (Ruby, Python, Javascript и т.д.). Но что если у вас есть пользовательский файл? Вам нужно научить Vim обнаруживать его и назначать ему правильный тип файла.

Существует два метода обнаружения: использование имени файла и содержимого файла.

### Обнаружение по имени файла

Обнаружение по имени файла определяет тип файла, используя имя этого файла. Когда вы открываете файл `hello.rb`, Vim знает, что это Ruby файл по расширению `.rb`.

Существует два способа, как вы можете сделать обнаружение по имени файла: используя директорию выполнения `ftdetect/` и используя файл выполнения `filetype.vim`. Давайте рассмотрим оба.

#### `ftdetect/`

Давайте создадим неясный (но вкусный) файл, `hello.chocodonut`. Когда вы открываете его и выполняете `:set filetype?`, поскольку это не распространенное расширение имени файла, Vim не знает, что с этим делать. Он возвращает `filetype=`.

Вам нужно указать Vim установить все файлы, заканчивающиеся на `.chocodonut`, как файлы типа "chocodonut". Создайте директорию с именем `ftdetect/` в корне выполнения (`~/.vim/`). Внутри создайте файл и назовите его `chocodonut.vim` (`~/.vim/ftdetect/chocodonut.vim`). Внутри этого файла добавьте:

```shell
autocmd BufNewFile,BufRead *.chocodonut set filetype=chocodonut
```

`BufNewFile` и `BufRead` срабатывают каждый раз, когда вы создаете новый буфер и открываете новый буфер. `*.chocodonut` означает, что это событие будет срабатывать только если открытый буфер имеет расширение имени файла `.chocodonut`. Наконец, команда `set filetype=chocodonut` устанавливает тип файла как тип chocodonut.

Перезапустите Vim. Теперь откройте файл `hello.chocodonut` и выполните `:set filetype?`. Он возвращает `filetype=chocodonut`.

Восхитительно! Вы можете поместить столько файлов, сколько хотите, внутри `ftdetect/`. В будущем вы можете, возможно, добавить `ftdetect/strawberrydonut.vim`, `ftdetect/plaindonut.vim` и т.д., если когда-либо решите расширить свои типы файлов для пончиков.

На самом деле, существует два способа установить тип файла в Vim. Один — это то, что вы только что использовали `set filetype=chocodonut`. Другой способ — выполнить `setfiletype chocodonut`. Первая команда `set filetype=chocodonut` *всегда* установит тип файла как тип chocodonut, в то время как вторая команда `setfiletype chocodonut` установит тип файла только если тип файла еще не был установлен.

#### Файл типа файла

Второй метод обнаружения файла требует от вас создания `filetype.vim` в корневой директории (`~/.vim/filetype.vim`). Добавьте это внутрь:

```shell
autocmd BufNewFile,BufRead *.plaindonut set filetype=plaindonut
```

Создайте файл `hello.plaindonut`. Когда вы открываете его и выполняете `:set filetype?`, Vim отображает правильный пользовательский тип файла `filetype=plaindonut`.

Святой десерт, это работает! Кстати, если вы поиграете с `filetype.vim`, вы можете заметить, что этот файл выполняется несколько раз, когда вы открываете `hello.plaindonut`. Чтобы предотвратить это, вы можете добавить защиту, чтобы основной скрипт выполнялся только один раз. Обновите `filetype.vim`:

```shell
if exists("did_load_filetypes")
  finish
endif

augroup donutfiletypedetection
  autocmd! BufRead,BufNewFile *.plaindonut setfiletype plaindonut
augroup END
```

`finish` — это команда Vim для остановки выполнения остальной части скрипта. Выражение `"did_load_filetypes"` *не* является встроенной функцией Vim. Это на самом деле глобальная переменная из `$VIMRUNTIME/filetype.vim`. Если вам интересно, выполните `:e $VIMRUNTIME/filetype.vim`. Вы найдете эти строки внутри:

```shell
if exists("did_load_filetypes")
  finish
endif

let did_load_filetypes = 1
```

Когда Vim вызывает этот файл, он определяет переменную `did_load_filetypes` и устанавливает ее в 1. 1 является истинным в Vim. Вам стоит прочитать остальную часть `filetype.vim` тоже. Посмотрите, сможете ли вы понять, что она делает, когда Vim вызывает ее.

### Скрипт типа файла

Давайте научимся определять и назначать тип файла на основе содержимого файла.

Предположим, у вас есть коллекция файлов без согласованного расширения. Единственное, что объединяет эти файлы, это то, что все они начинаются со слова "donutify" на первой строке. Вы хотите назначить этим файлам тип `donut`. Создайте новые файлы с именами `sugardonut`, `glazeddonut` и `frieddonut` (без расширения). Внутри каждого файла добавьте эту строку:

```shell
donutify
```

Когда вы выполняете `:set filetype?` из файла `sugardonut`, Vim не знает, какой тип файла назначить этому файлу. Он возвращает `filetype=`.

В корневом пути выполнения добавьте файл `scripts.vim` (`~/.vim/scripts.vim`). Внутри добавьте следующее:

```shell
if did_filetype()
  finish
endif

if getline(1) =~ '^\\<donutify\\>'
  setfiletype donut
endif
```

Функция `getline(1)` возвращает текст на первой строке. Она проверяет, начинается ли первая строка со слова "donutify". Функция `did_filetype()` — это встроенная функция Vim. Она вернет true, когда событие, связанное с типом файла, будет вызвано хотя бы один раз. Она используется как защита, чтобы остановить повторное выполнение события типа файла.

Откройте файл `sugardonut` и выполните `:set filetype?`, теперь Vim возвращает `filetype=donut`. Если вы откроете другие файлы пончиков (`glazeddonut` и `frieddonut`), Vim также определяет их типы файлов как типы `donut`.

Обратите внимание, что `scripts.vim` выполняется только тогда, когда Vim открывает файл с неизвестным типом файла. Если Vim открывает файл с известным типом файла, `scripts.vim` не будет выполняться.

## Плагин типа файла

Что если вы хотите, чтобы Vim выполнял скрипты, специфичные для chocodonut, когда вы открываете файл типа chocodonut, и не выполнял эти скрипты при открытии файла типа plaindonut?

Вы можете сделать это с помощью пути выполнения плагина типа файла (`~/.vim/ftplugin/`). Vim ищет внутри этой директории файл с тем же именем, что и тип файла, который вы только что открыли. Создайте файл `chocodonut.vim` (`~/.vim/ftplugin/chocodonut.vim`):

```shell
echo "Calling from chocodonut ftplugin"
```

Создайте другой файл ftplugin, `plaindonut.vim` (`~/.vim/ftplugin/plaindonut.vim`):

```shell
echo "Calling from plaindonut ftplugin"
```

Теперь каждый раз, когда вы открываете файл типа chocodonut, Vim выполняет скрипты из `~/.vim/ftplugin/chocodonut.vim`. Каждый раз, когда вы открываете файл типа plaindonut, Vim выполняет скрипты из `~/.vim/ftplugin/plaindonut.vim`.

Одно предупреждение: эти файлы выполняются каждый раз, когда устанавливается тип файла буфера (`set filetype=chocodonut`, например). Если вы откроете 3 разных файла chocodonut, скрипты будут выполнены *всего* трижды.

## Файлы отступов

У Vim есть путь выполнения отступов, который работает аналогично ftplugin, где Vim ищет файл с тем же именем, что и открытый тип файла. Цель этих путей выполнения отступов — хранить коды, связанные с отступами. Если у вас есть файл `~/.vim/indent/chocodonut.vim`, он будет выполняться только тогда, когда вы открываете файл типа chocodonut. Вы можете хранить коды, связанные с отступами для файлов chocodonut здесь.

## Цвета

У Vim есть путь выполнения цветов (`~/.vim/colors/`), чтобы хранить цветовые схемы. Любой файл, который попадает в директорию, будет отображаться в команде `:color`.

Если у вас есть файл `~/.vim/colors/beautifulprettycolors.vim`, когда вы выполняете `:color` и нажимаете Tab, вы увидите `beautifulprettycolors` как один из вариантов цвета. Если вы хотите добавить свою собственную цветовую схему, это то место, куда стоит обратиться.

Если вы хотите посмотреть цветовые схемы, созданные другими людьми, хорошее место для посещения — это [vimcolors](https://vimcolors.com/).

## Подсветка синтаксиса

У Vim есть путь выполнения синтаксиса (`~/.vim/syntax/`), чтобы определить подсветку синтаксиса.

Предположим, у вас есть файл `hello.chocodonut`, внутри которого есть следующие выражения:

```shell
(donut "tasty")
(donut "savory")
```

Хотя теперь Vim знает правильный тип файла, все тексты имеют один и тот же цвет. Давайте добавим правило подсветки синтаксиса, чтобы выделить ключевое слово "donut". Создайте новый файл синтаксиса для chocodonut, `~/.vim/syntax/chocodonut.vim`. Внутри добавьте:

```shell
syntax keyword donutKeyword donut

highlight link donutKeyword Keyword
```

Теперь снова откройте файл `hello.chocodonut`. Ключевые слова `donut` теперь подсвечиваются.

Эта глава не будет подробно рассматривать подсветку синтаксиса. Это обширная тема. Если вам интересно, посмотрите `:h syntax.txt`.

Плагин [vim-polyglot](https://github.com/sheerun/vim-polyglot) — отличный плагин, который предоставляет подсветку для многих популярных языков программирования.

## Документация

Если вы создаете плагин, вам нужно будет создать свою собственную документацию. Для этого вы используете путь выполнения doc.

Давайте создадим базовую документацию для ключевых слов chocodonut и plaindonut. Создайте файл `donut.txt` (`~/.vim/doc/donut.txt`). Внутри добавьте следующий текст:

```shell
*chocodonut* Вкусный шоколадный пончик

*plaindonut* Нет шоколадной доброты, но все равно вкусно
```

Если вы попытаетесь найти `chocodonut` и `plaindonut` (`:h chocodonut` и `:h plaindonut`), вы ничего не найдете.

Сначала вам нужно выполнить `:helptags`, чтобы сгенерировать новые записи помощи. Выполните `:helptags ~/.vim/doc/`

Теперь, если вы выполните `:h chocodonut` и `:h plaindonut`, вы найдете эти новые записи помощи. Обратите внимание, что файл теперь доступен только для чтения и имеет тип файла "help".
## Ленивое Загрузка Скриптов

Все пути выполнения, которые вы узнали в этой главе, выполнялись автоматически. Если вы хотите вручную загрузить скрипт, используйте путь выполнения автозагрузки.

Создайте директорию автозагрузки (`~/.vim/autoload/`). Внутри этой директории создайте новый файл и назовите его `tasty.vim` (`~/.vim/autoload/tasty.vim`). Внутри него:

```shell
echo "tasty.vim global"

function tasty#donut()
  echo "tasty#donut"
endfunction
```

Обратите внимание, что имя функции — `tasty#donut`, а не `donut()`. Символ решетки (`#`) обязателен при использовании функции автозагрузки. Конвенция именования функций для функции автозагрузки следующая:

```shell
function fileName#functionName()
  ...
endfunction
```

В данном случае имя файла — `tasty.vim`, а имя функции — (технически) `donut`.

Чтобы вызвать функцию, вам нужна команда `call`. Давайте вызовем эту функцию с помощью `:call tasty#donut()`.

В первый раз, когда вы вызываете функцию, вы должны увидеть *обе* сообщения эха ("tasty.vim global" и "tasty#donut"). Последующие вызовы функции `tasty#donut` будут отображать только сообщение "testy#donut".

Когда вы открываете файл в Vim, в отличие от предыдущих путей выполнения, скрипты автозагрузки не загружаются автоматически. Только когда вы явно вызываете `tasty#donut()`, Vim ищет файл `tasty.vim` и загружает все внутри него, включая функцию `tasty#donut()`. Автозагрузка — это идеальный механизм для функций, которые используют большие ресурсы, но вы не используете их часто.

Вы можете добавить столько вложенных директорий с автозагрузкой, сколько хотите. Если у вас есть путь выполнения `~/.vim/autoload/one/two/three/tasty.vim`, вы можете вызвать функцию с помощью `:call one#two#three#tasty#donut()`.

## Скрипты После

Vim имеет путь выполнения после (`~/.vim/after/`), который отражает структуру `~/.vim/`. Все, что находится в этом пути, выполняется последним, поэтому разработчики обычно используют эти пути для переопределения скриптов.

Например, если вы хотите переопределить скрипты из `plugin/chocolate.vim`, вы можете создать `~/.vim/after/plugin/chocolate.vim`, чтобы поместить скрипты переопределения. Vim выполнит `~/.vim/after/plugin/chocolate.vim` *после* `~/.vim/plugin/chocolate.vim`.

## $VIMRUNTIME

Vim имеет переменную окружения `$VIMRUNTIME` для стандартных скриптов и файлов поддержки. Вы можете проверить это, выполнив `:e $VIMRUNTIME`.

Структура должна выглядеть знакомо. Она содержит множество путей выполнения, которые вы узнали в этой главе.

Вспомните, в главе 21 вы узнали, что когда вы открываете Vim, он ищет файлы vimrc в семи разных местах. Я сказал, что последнее место, которое проверяет Vim, — это `$VIMRUNTIME/defaults.vim`. Если Vim не находит никаких пользовательских файлов vimrc, Vim использует `defaults.vim` в качестве vimrc.

Вы когда-нибудь пытались запустить Vim без плагина синтаксиса, такого как vim-polyglot, и все же ваш файл все еще синтаксически подсвечен? Это происходит потому, что когда Vim не может найти файл синтаксиса из пути выполнения, Vim ищет файл синтаксиса в директории синтаксиса `$VIMRUNTIME`.

Чтобы узнать больше, проверьте `:h $VIMRUNTIME`.

## Опция Runtimepath

Чтобы проверить ваш runtimepath, выполните `:set runtimepath?`

Если вы используете Vim-Plug или популярные внешние менеджеры плагинов, он должен отобразить список директорий. Например, у меня отображается:

```shell
runtimepath=~/.vim,~/.vim/plugged/vim-signify,~/.vim/plugged/base16-vim,~/.vim/plugged/fzf.vim,~/.vim/plugged/fzf,~/.vim/plugged/vim-gutentags,~/.vim/plugged/tcomment_vim,~/.vim/plugged/emmet-vim,~/.vim/plugged/vim-fugitive,~/.vim/plugged/vim-sensible,~/.vim/plugged/lightline.vim, ...
```

Одной из задач менеджеров плагинов является добавление каждого плагина в путь выполнения. Каждый путь выполнения может иметь свою собственную структуру директорий, аналогичную `~/.vim/`.

Если у вас есть директория `~/box/of/donuts/` и вы хотите добавить эту директорию в ваш путь выполнения, вы можете добавить это в ваш vimrc:

```shell
set rtp+=$HOME/box/of/donuts/
```

Если внутри `~/box/of/donuts/` у вас есть директория плагина (`~/box/of/donuts/plugin/hello.vim`) и ftplugin (`~/box/of/donuts/ftplugin/chocodonut.vim`), Vim выполнит все скрипты из `plugin/hello.vim`, когда вы откроете Vim. Vim также выполнит `ftplugin/chocodonut.vim`, когда вы откроете файл chocodonut.

Попробуйте это сами: создайте произвольный путь и добавьте его в ваш runtimepath. Добавьте некоторые из путей выполнения, которые вы узнали в этой главе. Убедитесь, что они работают так, как ожидалось.

## Учите Runtime Умным Способом

Не спешите, читайте это и экспериментируйте с этими путями выполнения. Чтобы увидеть, как пути выполнения используются в реальной жизни, перейдите в репозиторий одного из ваших любимых плагинов Vim и изучите его структуру директорий. Теперь вы должны понимать большинство из них. Попробуйте следовать за этим и разглядеть общую картину. Теперь, когда вы понимаете структуру директорий Vim, вы готовы изучать Vimscript.