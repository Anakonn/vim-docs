---
description: Документ описывает переменные в Vim, включая изменяемые и неизменяемые
  переменные, а также источники и области видимости переменных.
title: Ch27. Vimscript Variable Scopes
---

Перед тем как углубиться в функции Vimscript, давайте узнаем о различных источниках и областях видимости переменных Vim.

## Изменяемые и неизменяемые переменные

Вы можете присвоить значение переменной в Vim с помощью `let`:

```shell
let pancake = "pancake"
```

Позже вы можете вызывать эту переменную в любое время.

```shell
echo pancake
" возвращает "pancake"
```

`let` является изменяемым, что означает, что вы можете изменить значение в любое время в будущем.

```shell
let pancake = "pancake"
let pancake = "not waffles"

echo pancake
" возвращает "not waffles"
```

Обратите внимание, что когда вы хотите изменить значение установленной переменной, вам все равно нужно использовать `let`.

```shell
let beverage = "milk"

beverage = "orange juice"
" вызывает ошибку
```

Вы можете определить неизменяемую переменную с помощью `const`. Будучи неизменяемой, после присвоения значения переменной вы не можете переопределить ее другим значением.

```shell
const waffle = "waffle"
const waffle = "pancake"
" вызывает ошибку
```

## Источники переменных

Существует три источника для переменных: переменная окружения, переменная опции и переменная регистра.

### Переменная окружения

Vim может получить доступ к переменной окружения вашего терминала. Например, если у вас есть доступная переменная окружения `SHELL` в вашем терминале, вы можете получить к ней доступ из Vim с помощью:

```shell
echo $SHELL
" возвращает значение $SHELL. В моем случае это /bin/bash
```

### Переменная опции

Вы можете получить доступ к опциям Vim с помощью `&` (это настройки, к которым вы обращаетесь с помощью `set`).

Например, чтобы увидеть, какой фон использует Vim, вы можете выполнить:

```shell
echo &background
" возвращает либо "light", либо "dark"
```

Кроме того, вы всегда можете выполнить `set background?`, чтобы увидеть значение опции `background`.

### Переменная регистра

Вы можете получить доступ к регистрациям Vim (Гл. 08) с помощью `@`.

Предположим, что значение "chocolate" уже сохранено в регистре a. Чтобы получить к нему доступ, вы можете использовать `@a`. Вы также можете обновить его с помощью `let`.

```shell
echo @a
" возвращает chocolate

let @a .= " donut"

echo @a
" возвращает "chocolate donut"
```

Теперь, когда вы вставляете из регистра `a` (`"ap`), он вернет "chocolate donut". Оператор `.=` объединяет две строки. Выражение `let @a .= " donut"` эквивалентно `let @a = @a . " donut"`

## Области видимости переменных

Существует 9 различных областей видимости переменных в Vim. Вы можете распознать их по предшествующей букве:

```shell
g:           Глобальная переменная
{nothing}    Глобальная переменная
b:           Переменная локальная для буфера
w:           Переменная локальная для окна
t:           Переменная локальная для вкладки
s:           Переменная Vimscript из источника
l:           Локальная переменная функции
a:           Формальная переменная параметра функции
v:           Встроенная переменная Vim
```

### Глобальная переменная

Когда вы объявляете "обычную" переменную:

```shell
let pancake = "pancake"
```

`pancake` на самом деле является глобальной переменной. Когда вы определяете глобальную переменную, вы можете вызывать ее откуда угодно.

Предшествование `g:` переменной также создает глобальную переменную.

```shell
let g:waffle = "waffle"
```

В этом случае как `pancake`, так и `g:waffle` имеют одинаковую область видимости. Вы можете вызывать каждую из них с `g:` или без него.

```shell
echo pancake
" возвращает "pancake"

echo g:pancake
" возвращает "pancake"

echo waffle
" возвращает "waffle"

echo g:waffle
" возвращает "waffle"
```

### Переменная буфера

Переменная, предшествующая `b:`, является переменной буфера. Переменная буфера — это переменная, которая локальна для текущего буфера (Гл. 02). Если у вас открыто несколько буферов, каждый буфер будет иметь свой собственный отдельный список переменных буфера.

В буфере 1:

```shell
const b:donut = "chocolate donut"
```

В буфере 2:

```shell
const b:donut = "blueberry donut"
```

Если вы выполните `echo b:donut` из буфера 1, он вернет "chocolate donut". Если вы выполните его из буфера 2, он вернет "blueberry donut".

Кстати, у Vim есть *специальная* переменная буфера `b:changedtick`, которая отслеживает все изменения, внесенные в текущий буфер.

1. Выполните `echo b:changedtick` и запомните число, которое он возвращает.
2. Внесите изменения в Vim.
3. Выполните `echo b:changedtick` снова и запомните число, которое он теперь возвращает.

### Переменная окна

Переменная, предшествующая `w:`, является переменной окна. Она существует только в этом окне.

В окне 1:

```shell
const w:donut = "chocolate donut"
```

В окне 2:

```shell
const w:donut = "raspberry donut"
```

В каждом окне вы можете вызвать `echo w:donut`, чтобы получить уникальные значения.

### Переменная вкладки

Переменная, предшествующая `t:`, является переменной вкладки. Она существует только в этой вкладке.

В вкладке 1:

```shell
const t:donut = "chocolate donut"
```

В вкладке 2:

```shell
const t:donut = "blackberry donut"
```

В каждой вкладке вы можете вызвать `echo t:donut`, чтобы получить уникальные значения.

### Переменная скрипта

Переменная, предшествующая `s:`, является переменной скрипта. Эти переменные могут быть доступны только изнутри этого скрипта.

Если у вас есть произвольный файл `dozen.vim`, и внутри него у вас есть:

```shell
let s:dozen = 12

function Consume()
  let s:dozen -= 1
  echo s:dozen " осталось"
endfunction
```

Подключите файл с помощью `:source dozen.vim`. Теперь вызовите функцию `Consume`:

```shell
:call Consume()
" возвращает "11 осталось"

:call Consume()
" возвращает "10 осталось"

:echo s:dozen
" Ошибка неопределенной переменной
```

Когда вы вызываете `Consume`, вы видите, что значение `s:dozen` уменьшается, как и ожидалось. Когда вы пытаетесь получить значение `s:dozen` напрямую, Vim не найдет его, потому что вы находитесь вне области видимости. `s:dozen` доступна только изнутри `dozen.vim`.

Каждый раз, когда вы подключаете файл `dozen.vim`, он сбрасывает счетчик `s:dozen`. Если вы находитесь в процессе уменьшения значения `s:dozen` и выполните `:source dozen.vim`, счетчик сбросится обратно на 12. Это может быть проблемой для непосвященных пользователей. Чтобы исправить эту проблему, переработайте код:

```shell
if !exists("s:dozen")
  let s:dozen = 12
endif

function Consume()
  let s:dozen -= 1
  echo s:dozen
endfunction
```

Теперь, когда вы подключаете `dozen.vim`, находясь в процессе уменьшения, Vim проверяет `!exists("s:dozen")`, находит, что это правда, и не сбрасывает значение обратно на 12.

### Локальная переменная функции и формальная переменная параметра функции

Как локальная переменная функции (`l:`), так и формальная переменная функции (`a:`) будут рассмотрены в следующей главе.

### Встроенные переменные Vim

Переменная, предшествующая `v:`, является специальной встроенной переменной Vim. Вы не можете определять эти переменные. Некоторые из них вы уже видели.
- `v:version` сообщает вам, какую версию Vim вы используете.
- `v:key` содержит текущее значение элемента при итерации по словарю.
- `v:val` содержит текущее значение элемента при выполнении операции `map()` или `filter()`.
- `v:true`, `v:false`, `v:null` и `v:none` — это специальные типы данных.

Существуют и другие переменные. Для получения списка встроенных переменных Vim обратитесь к `:h vim-variable` или `:h v:`.

## Умное использование областей видимости переменных Vim

Возможность быстро получать доступ к переменным окружения, опций и регистров дает вам широкую гибкость для настройки вашего редактора и окружения терминала. Вы также узнали, что у Vim есть 9 различных областей видимости переменных, каждая из которых существует в определенных ограничениях. Вы можете воспользоваться этими уникальными типами переменных, чтобы разъединить вашу программу.

Вы дошли до этого момента. Вы узнали о типах данных, способах комбинаций и областях видимости переменных. Осталось только одно: функции.